
data { 
  int<lower=0> N;   // number of observations
  int<lower=1> p;   // number of predictors
  int<lower=1> J;   // number of groups in data (persons)
  int<lower=1> k;   // number of group-level predictors
  int<lower=1,upper=J> group[N]; //group indicator
  matrix[N,p] X;    // fixed-effect design matrix
  matrix[N,k] Z;    // random-effect design matrix
  vector[N] Y;        // response
} 

transformed data { 
  // Centering data for more stable sampling 
  int Pc; 
  matrix[N, p - 1] Xc;    // X centered
  matrix[N, p - 1] Xp;    // X without intercept, non-centered
  vector[p - 1] means_X;  // column means of X before centering 

  Pc = p - 1;  // the intercept is removed from the design matrix 
  for (i in 2:p) { 
     means_X[i - 1] = mean(X[, i]); 
     Xc[, i - 1] = X[, i] - means_X[i - 1]; 
     Xp[, i - 1] = X[, i]; 
  } 
}

parameters { 
  real temp_Intercept;            // temporary intercept 
  vector[Pc] beta;                // population-level effects (fixed effects)
  cholesky_factor_corr[k] L;      // Cholesky factor of group ranef corr matrix
  vector<lower=0>[k] sigma_b;     // group-level random-effect standard deviations
  real<lower=0> logsigma_e;       // residual standard deviations 
  vector[k] z[J];                 // unscaled group-level effects
  
  real<lower=0, upper=1000> g_alpha; // alpha (shape) parameter of the gamma distribution
}

transformed parameters {
  
  // Latent variables
  vector[k] b[J];                 // group-level effects (random effects)
  matrix[k, k] Sigma_b;           // variance-covariance matrix of group-level effects
  real<lower=0> sigma_e;          // residual standard deviations 

  // Premultiply diagonal matrix [sigma_b] with the Cholesky decomposition L of
  // the correlation matrix Sigma_b to get variance-covariance matrix of group-level effects

  // diag(sigma_b) * L
  Sigma_b = diag_pre_multiply(sigma_b, L); 
  
  // Group-level effects are generated by multipying D (Sigma_b) with z 
  // that has standard normal distribution
    
  for(j in 1:J) 
    b[j] = Sigma_b * z[j];    
    
  // variance 
  sigma_e = 1 / g_alpha;
}

model { 
  vector[N] mu;   // local variable for mean of Normal distribution
  vector[N] g_beta;    // local variable for beta (rate) of Gamma distribution

  // Priors
  beta[1] ~ cauchy(0,10); // prior for the intercept following Gelman 2008

  for(i in 2:Pc)
   beta[i] ~ cauchy(0,2.5); //prior for the slopes following Gelman 2008
   
  sigma_b ~ student_t(3, 0, 10);
  L ~ lkj_corr_cholesky(1);
  
  // Standard normal prior for random effects
  for (j in 1:J)
    z[j] ~ normal(0,1);

  // Likelihood 

  // - mean, or typical correlation
  mu = temp_Intercept + Xc * beta;
  
  // - add group effects
  for (n in 1:N) 
  {
     mu[n] += Z[n] * b[group[n]]; 
  }
  
  // - identity link
  g_beta = g_alpha ./ mu;

  Y ~ gamma(g_alpha, g_beta);

  
}

generated quantities { 
  real beta_Intercept;            // population-level intercept 
  corr_matrix[k] C;               // correlation matrix 
  vector[N] Y_rep;                // repeated response
  vector[N] log_lik;              // log-likelihood for LOO
  vector[k-1] personal_effect[J];

  // Correlation matrix of random-effects, C = L'L
  C = multiply_lower_tri_self_transpose(L); 
  
  beta_Intercept = temp_Intercept - dot_product(means_X, beta);
  
  // Posterior predictive distribution for model checking

  for (n in 1:N) 
  {
    // In Bayesian statistics, personal variation is part of mu, not variance
    Y_rep[n] = gamma_rng(g_alpha, g_alpha/(beta_Intercept + Xp[n] * beta + Z[n] * b[group[n]]));
    
    // Compute log-Likelihood for later LOO comparison of the models 
    log_lik[n] = gamma_lpdf(Y[n] | g_alpha, g_alpha/(beta_Intercept + Xp[n] * beta + Z[n] * b[group[n]]));
  }

  // Finally, sample personal effects for each nutrient
  for (j in 1:J) 
  {
    // beta vector does not include intercept, b is also sliced not to include it
    personal_effect[j] = beta + b[j][2:k];
  }
} 
